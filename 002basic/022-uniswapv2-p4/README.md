# Programming DeFi: Uniswap V2. Part 4

## Introduction

Welcome to the final part of this series! Yes, weâ€™ve almost done implementing a Uniswap V2 clone from scratch, and today weâ€™re going to fill missing gaps. Thereâ€™s a lot of things to do, so letâ€™s get straight to business.

æ¬¢è¿æ¥åˆ°æœ¬ç³»åˆ—çš„æœ€åä¸€éƒ¨åˆ†ï¼æ˜¯çš„ï¼Œæˆ‘ä»¬å‡ ä¹å·²ç»ä»å¤´å¼€å§‹å®ç°äº† Uniswap V2 å…‹éš†ï¼Œ ä»Šå¤©ï¼Œæˆ‘ä»¬å°†å¡«è¡¥ç¼ºå¤±çš„ç©ºç™½ã€‚æœ‰å¾ˆå¤šäº‹æƒ…è¦åšï¼Œæ‰€ä»¥è®©æˆ‘ä»¬ç›´æ¥è¿›å…¥æ­£é¢˜ã€‚

## LP-tokens burning bug

Weâ€™ll begin with finishing the Router contract. Last time we stopped at implementing `addLiquidity` function, and now weâ€™re going to implement `removeLiquidity`, which removes liquidity from a pool.

æˆ‘ä»¬å°†ä»å®Œæˆè·¯ç”±å™¨åˆåŒå¼€å§‹ã€‚ä¸Šæ¬¡æˆ‘ä»¬åœåœ¨å®ç°å‡½æ•°ä¸Š "addLiquidity" ï¼Œ ç°åœ¨æˆ‘ä»¬å°†å®ç° "removeLiquidity"ï¼Œå®ƒä»æ± ä¸­ç§»é™¤æµåŠ¨æ€§ã€‚

Before implementing `removeLiquidity`, we need to fix a bug in Pair contract. To understand the bug, letâ€™s look at `testBurn` function in Pair contract tests:

åœ¨å®ç°ä¹‹å‰"removeLiquidity"ï¼Œæˆ‘ä»¬éœ€è¦ä¿®å¤ Pair åˆçº¦ä¸­çš„ä¸€ä¸ªé”™è¯¯ã€‚è¦äº†è§£è¿™ä¸ªé”™è¯¯ï¼Œè®©æˆ‘ä»¬ æŸ¥çœ‹ "testBurn" ç»“å¯¹åˆçº¦æµ‹è¯•ä¸­çš„å‡½æ•°ï¼š

```solidity
function testBurn() public {
    token0.transfer(address(pair), 1 ether);
    token1.transfer(address(pair), 1 ether);

    pair.mint(address(this));
    pair.burn();

    assertEq(pair.balanceOf(address(this)), 0);
}
```

When someone provides liquidity to a pool, they get LP-tokens in return. When liquidity is removed, these LP-tokens are exchange for pool liquidity and burned. In this test, you can see that weâ€™re not transferring our LP-tokens to the pool, but itâ€™s still able to burn our tokens. If you now look in the `burn` function, youâ€™ll see this line

å½“æœ‰äººå‘èµ„é‡‘æ± æä¾›æµåŠ¨æ€§æ—¶ï¼Œä»–ä»¬ä¼šè·å¾— LP ä»£å¸ä½œä¸ºå›æŠ¥ã€‚å½“æµåŠ¨æ€§è¢«ç§»é™¤æ—¶ï¼Œè¿™äº› LPä»£å¸æ˜¯ç”¨äºäº¤æ¢æ± æµåŠ¨æ€§å¹¶è¢«çƒ§æ¯çš„ã€‚åœ¨æ­¤æµ‹è¯•ä¸­ï¼Œæ‚¨å¯ä»¥çœ‹åˆ°æˆ‘ä»¬æ²¡æœ‰è½¬ç§» æˆ‘ä»¬çš„ LP ä»£å¸åˆ°æ± ä¸­ï¼Œä½†å®ƒä»ç„¶èƒ½å¤Ÿé”€æ¯æˆ‘ä»¬çš„ä»£å¸ã€‚å¦‚æœä½ ç°åœ¨æŸ¥çœ‹å‡½æ•°ï¼Œ "burn" ä½ ä¼šçœ‹åˆ°è¿™ä¸€è¡Œ

```solidity
_burn(msg.sender, liquidity);
```

**The contract can burn senderâ€™s tokens without sender explicitly allowing that!** This is wrong. Instead, the user needs to have a way to tell the contract how much tokens to burnâ€“this can be achieved in two steps:

åˆçº¦å¯ä»¥åœ¨æ²¡æœ‰å‘é€æ–¹æ˜ç¡®å…è®¸çš„æƒ…å†µä¸‹é”€æ¯å‘é€æ–¹çš„ä»£å¸ï¼ è¿™æ˜¯é”™è¯¯çš„ã€‚å–è€Œä»£ä¹‹çš„æ˜¯ï¼Œ ç”¨æˆ·éœ€è¦æœ‰ä¸€ç§æ–¹æ³•æ¥å‘Šè¯‰åˆçº¦è¦é”€æ¯å¤šå°‘ä»£å¸â€”â€”è¿™å¯ä»¥é€šè¿‡ä¸¤ä¸ªæ­¥éª¤å®ç°ï¼š

1. Let user send some amount of their LP-tokens to the contract.è®©ç”¨æˆ·å‘åˆçº¦å‘é€ä¸€å®šæ•°é‡çš„LPä»£å¸ã€‚
2. Change the contract so it burns its LP-tokens.æ›´æ”¹åˆçº¦ï¼Œä½¿å…¶é”€æ¯å…¶ LP ä»£å¸ã€‚

You can find the fix of this bug in [this commit](https://github.com/Jeiwan/zuniswapv2/commit/babf8509b8be96796e2d944710bfcb22cc1fe77d#diff-835d3f34100b5508951336ba5a961932492eaa6923e3c5299f77007019bf2b6fR84).æ‚¨å¯ä»¥åœ¨æ­¤æäº¤ä¸­æ‰¾åˆ°æ­¤é”™è¯¯çš„ä¿®å¤ç¨‹åºã€‚

Now, weâ€™re ready to implement liquidity removal in the Router contract.ç°åœ¨ï¼Œæˆ‘ä»¬å‡†å¤‡åœ¨è·¯ç”±å™¨åˆçº¦ä¸­å®æ–½æµåŠ¨æ€§ç§»é™¤ã€‚



## Liquidity removal

Router contract is a high-level contract that makes interaction with Uniswap easier. As a result, its functions perform multiple actions, as opposed to the pair contract, in which functions perform only one core action. In addition to that, Routerâ€™s functions are generalâ€“they can be used to interact with any pair.

è·¯ç”±å™¨åˆçº¦æ˜¯ä¸€ç§é«˜çº§åˆçº¦ï¼Œä½¿ä¸ Uniswap çš„äº¤äº’æ›´å®¹æ˜“ã€‚å› æ­¤ï¼Œå…¶ å‡½æ•°æ‰§è¡Œå¤šä¸ªæ“ä½œï¼Œä¸å¯¹åå®šç›¸åï¼Œåœ¨å¯¹åå®šä¸­ï¼Œå‡½æ•°åªæ‰§è¡Œä¸€ä¸ªæ ¸å¿ƒ è¡ŒåŠ¨ã€‚é™¤æ­¤ä¹‹å¤–ï¼Œè·¯ç”±å™¨çš„åŠŸèƒ½æ˜¯é€šç”¨çš„â€”â€”å®ƒä»¬å¯ç”¨äºä¸ä»»ä½•ä¸€å¯¹è¿›è¡Œäº¤äº’ã€‚

So, we need a function that:

å› æ­¤ï¼Œæˆ‘ä»¬éœ€è¦ä¸€ä¸ªå‡½æ•°ï¼š

1. Abstracts away pairsâ€“users operate with tokens, not pairs.æŠ½è±¡å‡ºæˆå¯¹â€”â€”ç”¨æˆ·ä½¿ç”¨ä»¤ç‰Œè€Œä¸æ˜¯æˆå¯¹è¿›è¡Œæ“ä½œã€‚
2. Transfers userâ€™s LP-tokens to a pair contract. User needs to be able to select the amount.å°†ç”¨æˆ·çš„LPä»£å¸è½¬ç§»åˆ°å¯¹åˆçº¦ä¸­ã€‚ç”¨æˆ·éœ€è¦èƒ½å¤Ÿé€‰æ‹©é‡‘é¢ã€‚
3. Removes userâ€™s liquidity from a pair.ä»è´§å¸å¯¹ä¸­ç§»é™¤ç”¨æˆ·çš„æµåŠ¨æ€§ã€‚
4. Protects user from slippage. Yes, liquidity removal is also affected by slippageâ€“check out `testBurnUnbalancedDifferentUsers` test in Pair contract.ä¿æŠ¤ç”¨æˆ·å…å—æ»‘ç‚¹çš„å½±å“ã€‚æ˜¯çš„ï¼ŒæµåŠ¨æ€§ç§»é™¤ä¹Ÿå—åˆ°æ»‘ç‚¹çš„å½±å“ -åœ¨é…å¯¹åˆçº¦ä¸­ç­¾å‡ºæµ‹è¯•ã€‚

Letâ€™s implement this function:

```solidity
function removeLiquidity(
    address tokenA,
    address tokenB,
    uint256 liquidity,
    uint256 amountAMin,
    uint256 amountBMin,
    address to
) public returns (uint256 amountA, uint256 amountB) {
  ...
```

1. `tokenA`, `tokenB` are token addresses of the pair. Since users operate with tokens, they donâ€™t need to specify pair address.
2. `liquidity` is the amount of LP-tokens to burn.
3. `amountAMin`, `amountBMin` is the minimal amounts of tokenA and tokenB that we want to get in return for burning our LP-tokens. Itâ€™s these parameters that protect us from slippage.
4. `to` â€“ the address that will receive tokens.

First step is to find the pair:

```solidity
address pair = ZuniswapV2Library.pairFor(
    address(factory),
    tokenA,
    tokenB
);
```

Next: sending LP-tokens to the pair and burning the exact amount of tokens.

```solidity
IZuniswapV2Pair(pair).transferFrom(msg.sender, pair, liquidity);
(amountA, amountB) = IZuniswapV2Pair(pair).burn(to);
```

In the end, weâ€™re checking that amounts returned are within the tolerated slippage chosen by user.

```solidity
if (amountA < amountAMin) revert InsufficientAAmount();
if (amountB < amountBMin) revert InsufficientBAmount();
```

And, thatâ€™s it! Simple and elegant.

## Output amount calculation

Weâ€™re now approaching the moment when we can implement high-level swapping, including chained swapping (e.g. swap Token A for Token C via Token B). Before implementing it, we need to learn about how output amounts are calculated in Uniswap. Letâ€™s first figure out how amounts are related to prices.

What is price? Itâ€™s the amount of one thing you get in exchange for **1 unit** of another thing. When trading, price, in a sense, is an intermediate entity: what matters is the amount of tokens you have and the amount of tokens you get in return.

In a constant product exchange, price is simply a relation between reservesâ€“we already implement price calculation in the `quote` function of `ZuniswapV2Library`. However, when actually doing a swap, this price is incorrect because it represents only the relation between reserves at a moment. But when a swap is made, reserves are changed, and what we expect in fact is **the price to fall**, following the change in reserves.

To make this all clear, letâ€™s recall the constant product formula:
$$
x*y=k
$$


Where *x* and *y* are pair reserves (`reserve0` and `reserve1`).

When doing a swap, *x* and *y* are changed but *k* remains the same (or, actually, it grows slowly thanks to swap fees). We can write this as a formula:
$$
(x+r\Delta{x})(y-\Delta{y}) = xy
$$


Where *r* is `1 - swap fee` (1 - 0.3% = 0.997), Î”*x* is the amount we give in exchange for Î”*y*, the amount we get.

This is a very nice and concise formula that shows that the product of reserves after a swap must be equal to the product of reserves before the swap, which is the definition of the constant product formula. And we can use this formula to calculate the amount we get during a swap. After doing some basic algebraic operations, we get this:
$$
\Delta{y} = \frac {yr\Delta{x}}{x+r\Delta{x}}
$$


As you can see, this is a relation of two reserves (*y*/*x*) that takes into consideration the input amount (*r*Î”*x*), including the fee.

Letâ€™s now program this formula:

```solidity
function getAmountOut(
    uint256 amountIn,
    uint256 reserveIn,
    uint256 reserveOut
) public pure returns (uint256) {
  if (amountIn == 0) revert InsufficientAmount();
  if (reserveIn == 0 || reserveOut == 0) revert InsufficientLiquidity();
  ...
```

`amountIn` is Î”*x*, `reserveIn` is *x*, `reserveOut` is *y*.

Because of Solidity not supporting float point division, we need to multiply numerator and denominator by 1000 and then subtract 3 from the multiplier applied to `amountIn`â€“this will apply the 0.3% fee to `amountIn`:

```solidity
uint256 amountInWithFee = amountIn * 997;
uint256 numerator = amountInWithFee * reserveOut;
uint256 denominator = (reserveIn * 1000) + amountInWithFee;

return numerator / denominator;
```

And this is it! Now we can proceed with swapping.

## swapExactTokensForTokens

The Router contract implements different ways of swapping tokens. The most common of them is when we have an exact amount of tokens and want to get some, calculated, amount in exchange. Letâ€™s implement this:

Router åˆçº¦å®ç°äº†ä¸åŒçš„ä»£å¸äº¤æ¢æ–¹å¼ã€‚å…¶ä¸­æœ€å¸¸è§çš„æ˜¯å½“æˆ‘ä»¬æœ‰ä¸€ä¸ª ç¡®åˆ‡æ•°é‡çš„ä»£å¸ï¼Œå¹¶å¸Œæœ›è·å¾—ä¸€äº›è®¡ç®—å‡ºçš„é‡‘é¢ä½œä¸ºäº¤æ¢ã€‚è®©æˆ‘ä»¬å®ç°è¿™ä¸ªï¼š

```solidity
function swapExactTokensForTokens(
    uint256 amountIn,
    uint256 amountOutMin,
    address[] calldata path,
    address to
) public returns (uint256[] memory amounts) {
  ...
```

This is a function that swaps an exact input amount (`amountIn`) for some output amount not smaller than `amountOutMin`. It makes **chained swaps** along the specified `path` (which is simply a sequence of token addresses). The final amount is sent to address `to`.

è¿™æ˜¯ä¸€ä¸ªå°†ç²¾ç¡®çš„è¾“å…¥é‡ ï¼ˆ"amountIn"ï¼‰ æ¢æˆä¸å°äº "amountOutMin"ã€‚å®ƒ æ²¿ç€æŒ‡å®šçš„ï¼ˆè¿™åªæ˜¯ä¸€ä¸ªä»¤ç‰Œåºåˆ—ï¼‰è¿›è¡Œé“¾å¼äº¤æ¢"path" åœ°å€ï¼‰ã€‚æœ€ç»ˆé‡‘é¢å°†å‘é€åˆ°åœ°å€"to"ã€‚

The `path` parameter might seem like something complex, but itâ€™s just an array of token addresses. If we want to swap Token A for Token B directly, the path will contain only Token A and Token B addresses. If we want to swap Token A for Token C via Token B, the path will contain: Token A address, Token B address, Token C address; the contract would swap Token A for Token B and then Token B for Token C. Weâ€™ll see how this works in tests.

è¯¥ "path" å‚æ•°å¯èƒ½çœ‹èµ·æ¥å¾ˆå¤æ‚ï¼Œä½†å®ƒåªæ˜¯ä¸€ä¸ªä»¤ç‰Œåœ°å€æ•°ç»„ã€‚å¦‚æœæˆ‘ä»¬æƒ³è¦ è¦å°† Token A ç›´æ¥æ¢æˆ Token Bï¼Œè·¯å¾„å°†ä»…åŒ…å« Token A å’Œ Token B åœ°å€ã€‚å¦‚æœæˆ‘ä»¬æƒ³è¦ è¦é€šè¿‡ä»¤ç‰Œ B å°†ä»¤ç‰Œ A æ¢æˆä»¤ç‰Œ Cï¼Œè·¯å¾„å°†åŒ…å«ï¼šä»¤ç‰Œ A åœ°å€ã€ä»¤ç‰Œ B åœ°å€ã€ä»¤ç‰Œ C åœ°å€;åˆçº¦å°†ä»£å¸ A æ¢æˆä»£å¸ Bï¼Œç„¶åå°†ä»£å¸ B æ¢æˆä»£å¸ Cã€‚æˆ‘ä»¬å°†çœ‹çœ‹è¿™æ˜¯å¦‚ä½•å·¥ä½œçš„ åœ¨æµ‹è¯•ä¸­ã€‚

In the function, we begin with pre-calculating all output amounts along the path:

åœ¨å‡½æ•°ä¸­ï¼Œæˆ‘ä»¬é¦–å…ˆé¢„å…ˆè®¡ç®—è·¯å¾„ä¸Šçš„æ‰€æœ‰è¾“å‡ºé‡ï¼š

```solidity
amounts = ZuniswapV2Library.getAmountsOut(
    address(factory),
    amountIn,
    path
);
```

`getAmountsOut` (notice the plural â€œamountsâ€) is the new function that we havenâ€™t implemented yet. For brevity, I wonâ€™t explain its implementation, you can check the code to see it. This function simply extracts pairs of tokens from the path (e.g. `[[tokenA, tokenB], [tokenB, tokenC]]`) and then iteratively calls `getAmountOut` for each of them to build an array of output amounts.

"getAmountsOut" ï¼ˆæ³¨æ„å¤æ•°â€œamountsâ€ï¼‰æ˜¯æˆ‘ä»¬å°šæœªå®ç°çš„æ–°åŠŸèƒ½ã€‚ä¸º ç®€æ´ï¼Œæˆ‘å°±ä¸è§£é‡Šå®ƒçš„å®ç°äº†ï¼Œä½ å¯ä»¥æŸ¥ä¸€ä¸‹ä»£ç çœ‹çœ‹ã€‚æ­¤å‡½æ•°åªæ˜¯æå– æ¥è‡ªè·¯å¾„çš„æ ‡è®°å¯¹ï¼ˆä¾‹å¦‚"[[tokenA, tokenB], [tokenB, tokenC]]"ï¼‰ï¼Œç„¶åè¿­ä»£è°ƒç”¨ "getAmountOut" ä¸ºä»–ä»¬æ¯ä¸ªäººæ„å»ºä¸€ä¸ªè¾“å‡ºé‡æ•°ç»„ã€‚

After obtaining output amounts, we can verify the final amount right away:

```solidity
if (amounts[amounts.length - 1] < amountOutMin)
    revert InsufficientOutputAmount();
```

If the final amount is good, the contract initializes a swap by sending input tokens to the first pair:

```solidity
_safeTransferFrom(
    path[0],
    msg.sender,
    ZuniswapV2Library.pairFor(address(factory), path[0], path[1]),
    amounts[0]
);
```

And then it performs chained swaps:

```solidity
_swap(amounts, path, to);
```

Letâ€™s look at this function closely: the function takes an array of **output** amounts and a path, and iterates over the path.

```solidity
function _swap(
    uint256[] memory amounts,
    address[] memory path,
    address to_
) internal {
    for (uint256 i; i < path.length - 1; i++) {
      ...
```

It takes current and next token address from the path and sorts them. Sorting is required because, in pair contracts, token addresses are stored in ascending order, but, in the path, theyâ€™re sorted logically: input token goes first, then thereâ€™s 0 or multiple intermediate output tokens, then thereâ€™s final output token.

```solidity
(address input, address output) = (path[i], path[i + 1]);
(address token0, ) = ZuniswapV2Library.sortTokens(input, output);
```

Next, weâ€™re sorting amounts so they match the order of tokens in pairs. When doing a swap, we want to correctly choose output token.

```solidity
uint256 amountOut = amounts[i + 1];
(uint256 amount0Out, uint256 amount1Out) = input == token0
    ? (uint256(0), amountOut)
    : (amountOut, uint256(0));
```

After figuring out amounts, we need to find swap destination address. We have two options here:

1. If current pair is not final in the path, we want to send tokens to next pair directly. This allows to save gas.
2. If current pair is final, we want to send tokens to address `to_`, which is the address that initiated the swap.

```solidity
address to = i < path.length - 2
    ? ZuniswapV2Library.pairFor(
        address(factory),
        output,
        path[i + 2]
    )
    : to_;
```

After we obtained all the swap parameters, weâ€™re ready to make actual swap:

```solidity
IZuniswapV2Pair(
    ZuniswapV2Library.pairFor(address(factory), input, output)
).swap(amount0Out, amount1Out, to, "");
```

Weâ€™ve just implemented the core functionality of Uniswap! Congratulations! It wasnâ€™t that hard, right?

## swapTokensForExactTokens

The original Router contract [implements many different ways of swapping](https://github.com/Uniswap/v2-periphery/blob/master/contracts/UniswapV2Router02.sol#L224-L400). Weâ€™re not going to implement all of them, but I want to show you how an inverted swapping is implemented: swapping unknown amount of input tokens for exact amount of output tokens. This is an interesting use case and itâ€™s probably not used very often but itâ€™s still possible.

Letâ€™s return to the swapping formula:


$$
(x+r\Delta{x})(y-\Delta{y}) = xy
$$


Now, instead of Î”*y*, we want to find Î”*x*: we know the exact amount of output tokens we want to get but we donâ€™t know how much input tokens we need to give.

Again, after applying basic algebraic operations we get:
$$
\Delta{x} = \frac{x\Delta{x}}{(y-\Delta{x})r}
$$


And again, this is a relation of reserves (x*/*y*) that takes into consideration output amount (Î”*y) and fee *r*.

We can now implement this formula:

```solidity
function getAmountIn(
    uint256 amountOut,
    uint256 reserveIn,
    uint256 reserveOut
) public pure returns (uint256) {
    if (amountOut == 0) revert InsufficientAmount();
    if (reserveIn == 0 || reserveOut == 0) revert InsufficientLiquidity();

    uint256 numerator = reserveIn * amountOut * 1000;
    uint256 denominator = (reserveOut - amountOut) * 997;

    return (numerator / denominator) + 1;
}
```

Everything is clear, except the 1 added to the final resultâ€“why is that? The reason is that division, which is integer division, in Solidity rounds result down, which means that result gets truncated. In input amount calculation, we want to guarantee that the calculated amount will result in the requested `amountOut`. If result is truncated, output amount will be slightly smaller.

Next, we need `getAmountsIn` function:

```solidity
function getAmountsIn(
    address factory,
    uint256 amountOut,
    address[] memory path
) public returns (uint256[] memory) {
    if (path.length < 2) revert InvalidPath();
    uint256[] memory amounts = new uint256[](path.length);
    amounts[amounts.length - 1] = amountOut;

    for (uint256 i = path.length - 1; i > 0; i--) {
        (uint256 reserve0, uint256 reserve1) = getReserves(
            factory,
            path[i - 1],
            path[i]
        );
        amounts[i - 1] = getAmountIn(amounts[i], reserve0, reserve1);
    }

    return amounts;
}
```

It copies `getAmountsOut` with on significant change: the path is traversed in reversed order. Since we know the output amount and want to find input amounts, we start from the end of the path and fill `amounts` array with input amounts in reverse order.

The high-level swap function also looks very familiar:

```solidity
function swapTokensForExactTokens(
    uint256 amountOut,
    uint256 amountInMax,
    address[] calldata path,
    address to
) public returns (uint256[] memory amounts) {
    amounts = ZuniswapV2Library.getAmountsIn(
        address(factory),
        amountOut,
        path
    );
    if (amounts[amounts.length - 1] > amountInMax)
        revert ExcessiveInputAmount();
    _safeTransferFrom(
        path[0],
        msg.sender,
        ZuniswapV2Library.pairFor(address(factory), path[0], path[1]),
        amounts[0]
    );
    _swap(amounts, path, to);
}
```

Itâ€™s almost identical to the swap function we implemented earlier, but it calls `getAmountsIn` instead. Itâ€™s also interesting that we can use the same `_swap` function even when amounts are input ones.

Phew, weâ€™re done with the router contract! Thereâ€™s one more feature that Iâ€™d want to show you: flash loans.

## Fixing swap fee bug

Before we implement flash loans, thereâ€™s another bug that we need to fix. In the pair contract, we have these lines:

```solidity
uint256 balance0 = IERC20(token0).balanceOf(address(this)) - amount0Out;
uint256 balance1 = IERC20(token1).balanceOf(address(this)) - amount1Out;

if (balance0 * balance1 < uint256(reserve0_) * uint256(reserve1_))
    revert InvalidK();
```

This is a crucial check because it ensures that the swap was correct, i.e. correct input amount was provided and correct output amount was requested. However, these lines donâ€™t guarantee that swap fee was paid! The way the pair contract is currently implemented, there are no swap fees! Letâ€™s fix that.

Letâ€™s look closely at what weâ€™re doing in those lines:

1. First, weâ€™re getting current token balances (not reserves) of the pair contract.
2. Then we subtract output amounts from them because we expect to send those amounts to user.
3. We end up with balances that include input amount (sent to the contract by user) and exclude output amounts. They donâ€™t include swap fee.
4. Then, we calculate a new `k` and compare it with the previous one. The new one must be equal or bigger.

Itâ€™s clear that the new `k` considers only input and output amount, not the swap fee. And this is wrong.

To fix the bug, we need to rewrite the function.

First thing we do after obtaining reserves and doing pre-checks, is transferring tokens to users. Interesting that we can do this early, which makes this an optimistic operation (thereâ€™s a price for doing that, weâ€™ll take a look at it later). After transfers are made, we calculate input amounts:

```solidity
if (amount0Out > 0) _safeTransfer(token0, to, amount0Out);
if (amount1Out > 0) _safeTransfer(token1, to, amount1Out);

uint256 balance0 = IERC20(token0).balanceOf(address(this));
uint256 balance1 = IERC20(token1).balanceOf(address(this));

uint256 amount0In = balance0 > reserve0 - amount0Out
    ? balance0 - (reserve0 - amount0Out)
    : 0;
uint256 amount1In = balance1 > reserve1 - amount1Out
    ? balance1 - (reserve1 - amount1Out)
    : 0;

if (amount0In == 0 && amount1In == 0) revert InsufficientInputAmount();
```

To make the logic of this piece clear, we can think of `reserve0` and `reserve1` as â€œold balancesâ€, balances that the contract had before the swap started.

When swapping tokens, we usually provide either `amount0Out` or `amount1Out`. So, usually, there will be either `amount0In` or `amount1In` (the other one will be zero). But this piece (as well as the swap function) allows us to set both `amount0Out` and `amount1Out`, so itâ€™s also possible that both `amount0In` and `amount1In` will be greater than zero. But if both of them are zero, the user hasnâ€™t sent any tokens to the contract, which is not allowed.

So, in these lines, weâ€™re finding out new balances: they donâ€™t include output amounts but include input ones.

Next lines are fixed versions of the bugged implementation:

```solidity
uint256 balance0Adjusted = (balance0 * 1000) - (amount0In * 3);
uint256 balance1Adjusted = (balance1 * 1000) - (amount1In * 3);

if (
    balance0Adjusted * balance1Adjusted <
    uint256(reserve0_) * uint256(reserve1_) * (1000**2)
) revert InvalidK();
```

First, we calculate adjusted balances: these are **current balances minus swap fees**, which are applied to input amounts. Again, because of integer division, we have to multiply balances by 1000 and amounts by 3 to â€œemulateâ€ multiplication of the input amounts by 0.003 (0.3%).

Next, weâ€™re calculating a new *k* for the adjusted balances and comparing it to the current one. To compensate for the multiplication by 1000 in the adjusted balances, we multiply old reserves by 1000 * 1000.

Weâ€™re basically calculating a new *k* on new balances minus the swap fee. And this new *k* must be greater or equal to the old *k*.

Letâ€™s test the case when weâ€™re trying to get too much of output tokens and are getting the `InvalidK` error:

```solidity
function testSwapUnpaidFee() public {
    token0.transfer(address(pair), 1 ether);
    token1.transfer(address(pair), 2 ether);
    pair.mint(address(this));

    token0.transfer(address(pair), 0.1 ether);

    vm.expectRevert(encodeError("InvalidK()"));
    pair.swap(0, 0.181322178776029827 ether, address(this), "");
}
```

Here, weâ€™re trying to swap 0.1 ether of token0 for 0.181322178776029827 ether of token1 and fail. If you reduce the token1 amount by 1, the test will pass. I used `getAmountOut` to calculate this amountâ€“ feel free to experiment with it!

This is a tricky piece, you might need to experiment with tests and number to get it. Hopefully, itâ€™ll be clear in the end!

And now weâ€™re ready to implement flash loans ğŸƒ

## Flash loans

Everybody likes them! Or not? Well, maybe not the developers who got their contracts hacked using flash loans ğŸ¤­

æ¯ä¸ªäººéƒ½å–œæ¬¢ä»–ä»¬ï¼æˆ–ä¸ï¼Ÿå¥½å§ï¼Œä¹Ÿè®¸ä¸æ˜¯é‚£äº›ä½¿ç”¨é—ªç”µè´·ğŸ¤­è®©åˆåŒè¢«é»‘å®¢å…¥ä¾µçš„å¼€å‘å•†

Flash loans is a very powerful financial instrument that has no analogues in traditional finances. Itâ€™s an **unlimited** and **uncollateralized** loan that must be repaid in **the same transaction** where itâ€™s taken. Uniswap is one of the platforms thatâ€™s able to give flash loans. Letâ€™s add them to our contracts and see how they work.

é—ªç”µè´·æ˜¯ä¸€ç§éå¸¸å¼ºå¤§çš„é‡‘èå·¥å…·ï¼Œåœ¨ä¼ ç»Ÿé‡‘èä¸­æ²¡æœ‰ç±»ä¼¼ç‰©ã€‚è¿™æ˜¯ä¸€ä¸ª æ— é™åˆ¶å’Œæ— æŠµæŠ¼è´·æ¬¾ï¼Œå¿…é¡»åœ¨è·å¾—è´·æ¬¾çš„åŒä¸€ç¬”äº¤æ˜“ä¸­å¿è¿˜ã€‚ Uniswap æ˜¯èƒ½å¤Ÿæä¾›é—ªç”µè´·çš„å¹³å°ä¹‹ä¸€ã€‚è®©æˆ‘ä»¬å°†å®ƒä»¬æ·»åŠ åˆ°æˆ‘ä»¬çš„åˆåŒä¸­ï¼Œçœ‹çœ‹å¦‚ä½• ä»–ä»¬å·¥ä½œã€‚

First thing you need to know about flash loans implementation is that **they can only be used by smart contracts**. Hereâ€™s how borrowing and repaying happens with flash loans:

å…³äºé—ªç”µè´·çš„å®æ–½ï¼Œæ‚¨éœ€è¦äº†è§£çš„ç¬¬ä¸€ä»¶äº‹æ˜¯ï¼Œå®ƒä»¬åªèƒ½ç”±æ™ºèƒ½ç”¨æˆ·ä½¿ç”¨ åˆåŒã€‚ä»¥ä¸‹æ˜¯é—ªç”µè´·çš„å€Ÿæ¬¾å’Œè¿˜æ¬¾æ–¹å¼ï¼š

1. A smart contract borrows a flash loan from another contract.

   æ™ºèƒ½åˆçº¦ä»å¦ä¸€ä»½åˆçº¦å€Ÿå…¥é—ªç”µè´·ã€‚

2. The lender contract sends tokens to the borrowing contract and calls a special function in this contract.

   è´·æ–¹åˆçº¦å‘å€Ÿæ¬¾åˆçº¦å‘é€ä»£å¸ï¼Œå¹¶è°ƒç”¨è¯¥åˆçº¦ä¸­çš„ç‰¹æ®Šå‡½æ•°ã€‚

3. In the special function, the borrowing contract performs some operations with the loan and then transfers the loan back.

   åœ¨ç‰¹æ®ŠåŠŸèƒ½ä¸­ï¼Œå€Ÿæ¬¾åˆçº¦å¯¹å€Ÿæ¬¾è¿›è¡Œä¸€äº›æ“ä½œï¼Œç„¶åè¿›è¡Œè½¬è´¦ è´·æ¬¾è¿”è¿˜ã€‚

4. The lender contract ensures that the whole amount was paid back. In case when there are fees, it also ensures that they were paid.

   è´·æ–¹åˆåŒç¡®ä¿å…¨é¢å¿è¿˜ã€‚å¦‚æœæœ‰è´¹ç”¨ï¼Œå®ƒä¹Ÿ ç¡®ä¿ä»–ä»¬å¾—åˆ°æŠ¥é…¬ã€‚

5. Control flow returns to the borrowing contract.

   æ§åˆ¶æµè¿”å›åˆ°å€Ÿç”¨åˆåŒã€‚

To add flash loans to Zuniswap, we need to make a few changes. First, update `swap` to take an additional parameter:

```solidity
function swap(
    uint256 amount0Out,
    uint256 amount1Out,
    address to,
    bytes calldata data
) public {
```

The new parameter is a byte array `data`. It can contain literary anything.

Next step is to actually issue a loan. Remember that we have optimistic transfers in the `swap` function:

```solidity
...
if (amount0Out > 0) _safeTransfer(token0, to, amount0Out);
if (amount1Out > 0) _safeTransfer(token1, to, amount1Out);
...
```

And this means that weâ€™re already giving an arbitrary amount (the output amounts are specified by user) of tokens without asking for collateral! The only thing we need to change is to let the caller to repay the loan. Weâ€™re doing this by calling a special function in the caller contract:

```solidity
...
if (amount0Out > 0) _safeTransfer(token0, to, amount0Out);
if (amount1Out > 0) _safeTransfer(token1, to, amount1Out);
if (data.length > 0) IZuniswapV2Callee(to).zuniswapV2Call(msg.sender, amount0Out, amount1Out, data);
...
```

By the convention we define, we expect the caller contract to implement `zuniswapV2Call` function that receives: sender address, first output amount, second output amount, and the new data parameter. Everything else remains unchanged in the contract! Again, very elegant and simple solution.

And thatâ€™s basically it! It turns out, weâ€™ve already implemented the logic that checks if the loan was repaidâ€“ itâ€™s the same logic that checks that a new *k* is valid!

Now, letâ€™s test the flash loans! I hope the whole flow will be clearer after we add a test.

As I said above, to use flash loans we need a smart contract. And to test flash loans we need a separate contractâ€“letâ€™s call it `Flashloaner`:

```solidity
contract Flashloaner {
    error InsufficientFlashLoanAmount();

    uint256 expectedLoanAmount;

    ...
}
```

The contract will implement two functions:

1. First function will borrow a flash loan from Zuniswap.
2. Second function, `zuniswapV2Call`, will handle the loan and repay it.

Taking a flash loan is as easy as making a swap:

```solidity
function flashloan(
    address pairAddress,
    uint256 amount0Out,
    uint256 amount1Out,
    address tokenAddress
) public {
    if (amount0Out > 0) {
        expectedLoanAmount = amount0Out;
    }
    if (amount1Out > 0) {
        expectedLoanAmount = amount1Out;
    }

    ZuniswapV2Pair(pairAddress).swap(
        amount0Out,
        amount1Out,
        address(this),
        abi.encode(tokenAddress)
    );
}
```

Before making the swap, we want to set `expectedLoanAmount` so we can later check that requested amount of tokens was in fact given to us.

In the swap function, notice that weâ€™re passing `tokenAddress` as the `data` parameterâ€“weâ€™ll use it later to repay the loan. Alternatively, we couldâ€™ve stored this address in a state variable. Since data is a byte array, we need a way to convert an address to bytes, and `abi.encode` is a common solution for that.

Now, the flash loan handler:

```solidity
function zuniswapV2Call(
    address sender,
    uint256 amount0Out,
    uint256 amount1Out,
    bytes calldata data
) public {
    address tokenAddress = abi.decode(data, (address));
    uint256 balance = ERC20(tokenAddress).balanceOf(address(this));

    if (balance < expectedLoanAmount) revert InsufficientFlashLoanAmount();

    ERC20(tokenAddress).transfer(msg.sender, balance);
}
```

This is the function that will be called by the pair contract in the swap function that we call in `flashloan`. The pair contract will also pass whatever data we passed to the swap call.

In the handler function, weâ€™re ensuring that we in fact got the requested loan and weâ€™re simply paying it back. Instead of repaying it, we couldâ€™ve used it for something like [leveraging](https://medium.com/coinmonks/evaluating-defi-strategies-using-foundry-151bf8cb8759), arbitraging, or exploiting bugs in smart contracts. Flash loans is a very powerful instrument that can be used for the good or the bad.

Finally, letâ€™s add a test that takes a loan and ensures that a correct amount is repaid:

```solidity
function testFlashloan() public {
    token0.transfer(address(pair), 1 ether);
    token1.transfer(address(pair), 2 ether);
    pair.mint(address(this));

    uint256 flashloanAmount = 0.1 ether;
    uint256 flashloanFee = (flashloanAmount * 1000) / 997 - flashloanAmount + 1;

    Flashloaner fl = new Flashloaner();

    token1.transfer(address(fl), flashloanFee);

    fl.flashloan(address(pair), 0, flashloanAmount, address(token1));

    assertEq(token1.balanceOf(address(fl)), 0);
    assertEq(token1.balanceOf(address(pair)), 2 ether + flashloanFee);
}
```

The thing is, Uniswap V2 imposes fees on flash loans: **we must pay the swap fee on them**. Recall that we didnâ€™t implement any additional checks for whether a flash loan was repaid or notâ€“we simply used the new *k* calculation. And this calculation subtracts the swap fee from balances! So, when returning a flash loan we must pay the amount weâ€™ve taken + 0.3% (slightly above that actually: 0.3009027%).

For Flashloaner to repay full amount, we calculate `flashloanFee` and send it to the contract. After the flash loan is repaid, Flashloanerâ€™s balance is 0 and the pair contract gets the fee.

## Fixing re-entrancy vulnerability

And the final touch: with the new changes in the pair contract, we introduced a very dangerous vulnerabilityâ€“re-entrancy. We have already discussed this in a previous part: when implementing functions that make external calls, we must be very cautious to not make re-entrancy attacks possible. We have also discussed that the [Checks Effects Interactions pattern](https://fravoll.github.io/solidity-patterns/checks_effects_interactions.html) is one way of preventing the attack. However, in the rewritten `swap` function, we cannot use the pattern because the implementation forces us to make external calls (token transfers) before applying effects (updating reserves). We want the optimistic transfers and we like the simplicity of the flash loans implementation! So, we need a different protection. And itâ€™s not hard to implement.

In situations when the Checks Effects Interactions patterns cannot be applied, we can use [the Guard Check pattern](https://fravoll.github.io/solidity-patterns/guard_check.html): we simply need to add a flag that is set when `swap` function is called; and we wonâ€™t allow to call the function if flag is set. Hereâ€™s how to do this.

First, add the flag. Weâ€™ll call it `isEntered`:

```solidity
contract ZuniswapV2Pair is ERC20, Math {
    ...
    bool private isEntered;
    ...
}
```

Yes, itâ€™s stored in the contractâ€™s storage, which increases the gas cost of the swap function. And this is why Checks Effects Interactions pattern is better.

Next, we add a modifier:

```solidity
  modifier nonReentrant() {
      require(!isEntered);
      isEntered = true;

      _;

      isEntered = false;
  }
```

The modifier:

1. Ensures that the flag is not set.
2. Sets the flag.
3. Executes function body (`_` is replaced with the body of the function this modifier is applied to).
4. When the function is done, it unsets the flag.

Finally, we need to apply this modifier to the swap function:

```solidity
function swap(
    uint256 amount0Out,
    uint256 amount1Out,
    address to,
    bytes calldata data
) public nonReentrant {
    ...
}
```

Thatâ€™s it! Iâ€™ll leave it to you to test this vulnerability ğŸ˜‰

## Protocol fees

Last thing we havenâ€™t implemented is protocol fees.

Protocol fees are fees collected by Uniswap every time someone adds liquidity to a pool. These fees go directly to the Uniswap team, not to liquidity providers or traders. However, at the moment of writing, **protocol fees are not enabled, which means that the Uniswap team earns nothing from the exchange that they built**.

Since this post is already quite long, Iâ€™m not going to implement protocol fees in our clone. By this moment, Iâ€™m pretty sure youâ€™ll be able to implement them yourself! You can use [the original implementation](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L88-L107) as a reference. Also, PRs are welcomed ğŸ˜‰

## Conclusion

This is it, our journey has come to its end. I really hope you enjoyed it and learned a lot along the way. Uniswap V2 is a fantastic project that combines simplicity, elegance, and unique invention. Its code is a gift to us, it letâ€™s us see that a truly decentralized platform and an integral DeFi solution can be implemented as a set of simple and elegant smart contractsâ€“a role model for every Solidity developer!

------
